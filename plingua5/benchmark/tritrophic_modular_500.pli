@model<probabilistic>
@include "pdp_model.pli"

// change the number of species for benchmarking
M = 500 ; // Number of species
N = 10; // Number of environments

////////////////////////////////////
// definition of the transition graph
@modules="(1,1,{2});(2,1,{3});(3,5,{4});(4,1,{5});(5,1,{})";
// definition of the transition graph using names for modules
//@modules="(reproduction,1,{feedingMortality});(feedingMortality,1,{movement});(movement,5,{feeding});(feeding,1,{restoration});(restoration,1,{})";
////////////////////////////////////


def main() 
{
  /*******************************/
  /* Initial membrane structure  */
  /*******************************/
   
  @mu = []'p;
  @mu(p) += [[[]'1]'0]'{k},{k} :  101 <= k <= (100+N); 
   


  /*********************/
  /* Initial Multisets */
  /*********************/

  @ms(1,{j+100}) += X{k} * q{k,j} : 2<=k<=M,1<=j<=N; /* Herbivoros y carnivoros */
  @ms(0,{j+100}) += X{1} * q{1,j} : 1<=j<=N;         /* Hierba */
  @ms(0,{j+100}) += R{0}          : 1<=j<=N;         /* Objeto contador */

  /**********************/
  /* Set of rules       */
  /**********************/

  /**********************/
  /*      Module 1      */
  /**********************/

  /*r1*/ X{1}[]'1-->+[X{1},G*(h{j})]'1 :: m{j} @module="1": 1<=j<=3;   /* Grass production */

  /*r2*/ [X{i}]'1-->+[X{i}*(1+d{i})]'1 :: (k{i,1}*0.5) @module="1": 2<=i<=M; /* Animals in reproduction */
	
  /*r3*/ [X{i}]'1-->+[X{i}]'1 :: 1-(k{i,1}*0.5) @module="1": 2<=i<=M; /* Animals not reproducting */
	
  /*r4*/ R{0}[]'1-->+[R{0}]'1 ::1 @module="1"; /* Sincronization */

  /**********************/
  /*    Module 2        */
  /**********************/
	
  /*r5*/ +[X{i}, G*(f{i})]'1 --> -[Y{i}]'1 :: (1-k{i,2}) @module="2": 2<=i<=M-1; /* Herbívoros que se alimentan */

  /*r6*/ +[X{M}, X{i}*(f{M})]'1 --> -[Y{M}]'1 :: (1-k{M,2}) @module="2": 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r5*/ +[X{i}, G*(f{i})]'1 --> -[]'1 :: k{i,2} @module="2": 2<=i<=M-1; /* Herbívoros que se alimentan y mueren */

  /*r6*/ +[X{M}, X{i}*(f{M})]'1 --> -[]'1 :: k{M,2} @module="2": 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r9*/ +[R{0}]'1-->-[R{0}]'1 :: 1 @module="2"; /* Sincronización */
	
  /*****************/
  /*   Module 3    */
  /*****************/
	
 /*r10*/ -[X{i}]'1 --> X{i} -[]'1 :: 1 @module="3": 2<=i<=M; /* Los animales salen a la membrana piel */
	
 /*r11*/ [X{i}]'0 --> X{i} []'0 :: 1 @module="3": 2<=i<=M; /* Los animales salen al entorno */
	 
 /*re12*/ [[X{i}]'{k+100} [#]'{r+100}]'p --> 
          [[#]'{k+100} [Xp{i}]'{r+100}]'p :: p{k,r,i} @module="3": 1<=k<=N, 1<=r<=N, 2<=i<=M, k<>r; /* Desplazamiento entre entornos */
          
          [[X{i}]'{k+100}]'p --> 
          [[Xp{i}]'{k+100}]'p :: p{k,k,i} @module="3": 1<=k<=N, 2<=i<=M; /* Permanencia en el mismo entorno */	 


  /*r13*/ Xp{i} []'0-->[Xp{i}]'0 :: 1 @module="3": 2<=i<=M; /* Los animales entran en la membrana piel */
	
  /*r14*/ Xp{i} -[]'1 --> -[Xp{i}]'1 :: 1 @module="3": 2<=i<=M; /* Los animales entran en la membrana 1 */
	
  /*r15*/ -[R{i}]'1--> -[R{i+1}]'1 :: 1 @module="3": 0<=i<=4;
	

  /*****************/
  /*   Module 4    */
  /*****************/	

  /*r16*/ -[Xp{i}, G*(f{i})]'1 --> [Y{i}]'1 :: (1-k{i,2}) @module="4": 2<=i<=M-1; /* Herbívoros que se alimentan */
	
  /*r17*/ -[Xp{M}, Xp{i}*(f{M})]'1 --> [Y{M}]'1 :: (1-k{M,2}) @module="4": 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r18*/ -[Xp{M}, Y{i}*(f{M})]'1 --> [Y{M}]'1 :: (1-k{M,2}) @module="4": 2<=i<=M-1; /* Carnívoros que se alimentan */
	
  /*r19*/ -[Xp{i}, G*(f{i})]'1 --> [#]'1 :: k{i,2} @module="4": 2<=i<=M-1; /* Herbívoros que se alimentan y mueren */
	
  /*r20*/ -[Xp{M}, Xp{i}*(f{M})]'1 --> [#]'1 :: k{M,2} @module="4": 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r21*/ -[Xp{M}, Y{i}*(f{M})]'1 --> [#]'1 :: k{M,2} @module="4": 2<=i<=M-1; /* Carnívoros que se alimentan y mueren */
	
  /*r22*/ -[R{5}]'1-->[R{6}]'1 :: 1 @module="4"; /* Sincronización */
	
	
 /************/
 /* Module 5 */
 /************/
  /*r23*/[Y{i}]'1-->[X{i}]'1 :: 1 @module="5": 2<=i<=M;
	
  /*r24*/[R{6}]'1-->[R{0}]'1 :: 1 @module="5";
	
  /*r25*/ [X{1}]'1-->X{1}[#]'1 :: 1 @module="5";
	
  /*r26*/ [Xp{i}]'1-->[#]'1 :: 1 @module="5": 2<=i<=M;
	
  /*r27*/ [G]'1-->[#]'1 :: 1 @module="5";

}

// Initial grass 
q{1,j} = 1000 + random(1000) : 1<=j<=N;

// Initial species
q{i,j} = 100 + random(100) : 2<=i<=M, 1<=j<=N;

// Grass parameters
h{1} = 2920;
h{2} = 4390;
h{3} = 5475;
m{1} = 0.25;
m{2} = 0.6;
m{3} = 0.15;

// Offsprings
d{i} =  1 + random(2) : 1<=i<=M;

// Reproduction
k{i,1} = (double_t)random(1000)/(double_t)1000 : 2<=i<=M;


f{i} = 20 + random(80) : 2<=i<=M-1;
f{M} = 5+ random(5);

k{i,2} = (double_t)random(1000)/(double_t)1000 : 2<=i<=M;

p{k,r,i} = 1.0/(double_t)N : 1<=k<=N,1<=r<=N,1<=i<=M;

